plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.5.1'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

version '0.1'
group 'es.juanc.katas'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.16'
    annotationProcessor 'org.projectlombok:lombok:1.18.16'

    implementation 'org.slf4j:slf4j-api:1.7.18'
    implementation 'org.slf4j:slf4j-simple:1.7.21'

    implementation 'io.vavr:vavr:0.10.3'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0'
    testImplementation 'org.mockito:mockito-core:3.6.28'
    testImplementation 'org.assertj:assertj-core:3.18.1'
}

application {
    mainClassName = 'es.juanc.katas.marsrover.GroundControl'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/coverage")
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/GroundControl*'])
        }))
    }
}

pitest {
    junit5PluginVersion = '0.12'
    targetClasses = ['es.juanc.katas.marsrover.*']
    threads = 4
    jvmArgs = ['-Xmx1024m']
    outputFormats = ['HTML']
    timestampedReports = false
}
